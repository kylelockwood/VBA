Function NewestFile(directory, fileSpec)
' Returns the name of the most recent file in a Directory
' That matches the fileType (e.g., "*.xls").
' Returns an empty string if the directory does not exist or
' it contains no matching files
    Dim fileName As String
    Dim mostRecentFile As String
    Dim mostRecentDate As Date
    If Right(directory, 1) <> "\" Then directory = directory & "\"

    fileName = Dir(directory & fileSpec, 0)
    If fileName <> "" Then
        mostRecentFile = fileName
        mostRecentDate = FileDateTime(directory & fileName)
        Do While fileName <> ""
            If FileDateTime(directory & fileName) > MostRecentDate Then
                 mostRecentFile = fileName
                 mostRecentDate = FileDateTime(directory & fileName)
             End If
             fileName = Dir
        Loop
    End If
    NewestFile = mostRecentFile
End Function